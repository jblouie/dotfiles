#!/usr/bin/perl

=head1 NAME

    truncate - shorten the output of each line to a certain number of characters

=head1 SYNOPSIS

    truncate [options] <filename> | truncate [options] <STDIN>

    Options:
      --bare              Do not print "...[xx more]" at the end of overly-long lines.
      --length            Give a number of characters to print. In addition to these, by default
                          "...[xx more]" will also be printed at the end. The default line length
                          is 79 characters.

=cut

use warnings;
use strict;

use Getopt::Long;
use Pod::Usage qw/pod2usage/;

my($bare, $line_length);
$line_length = 79;

GetOptions(
           'bare'                 => \$bare,
           'help|?'               => sub { pod2usage() },
           'line_length|length=i' => \$line_length,
          );

while (<>) {

    if (length() < $line_length) {
        print;
        next;
    }

    my $leftover = length() - $line_length - 1;
    print substr($_, 0, $line_length);
    print "..[$leftover more]" unless $bare;
    print "\n";
}
