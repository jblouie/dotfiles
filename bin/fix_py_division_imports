#!/usr/bin/perl

use warnings;
use strict;

use File::Temp qw/tempfile/;
use Getopt::Long;
use File::Find;

foreach my $filename (@ARGV) {
    if( -T $filename) {
        fix_division_import($filename);
    }
    elsif( -d _ ) {
        find(\&wanted, $filename);
    }
}

############ END OF MAIN ################

sub wanted {
    return unless -T $_;
    return unless /\.py$/;

    fix_division_import($_);
}

sub fix_division_import {
    my $filename = shift;

    local $/;
    undef $/;
    open my $fh, '<', $filename or do {
        print STDERR "Cannot open $filename for reading: $!\n";
        return;
    };
    my $file_contents = <$fh>;
    close $fh;

    if( $file_contents =~ m{^from __future__ import division$}m and
        $file_contents !~ m{\s/\s} ) {

        my($out, $tmp_filename) = tempfile(UNLINK => 0);

        print "Fixing $filename\n";
        $file_contents =~ s{^from __future__ import division\n}{}mg;
        print $out $file_contents;
        $out->close;
        rename $tmp_filename, $filename;
        unlink $tmp_filename;
    }
}
