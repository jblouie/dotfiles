#!/usr/bin/perl

=head1 NAME

    git_scp - copy modified/added files from local machine to remote machine

=head1 SYNOPSIS

    git_scp [options] [regex]

    Script which transfers files from this computer to a remote computer based on settings.

    If STDIN is given, most of the command-line options are ignored. Each of the files
    is transferred.

    If STDIN is not given, the script will look at the output from a `git status` and
    will transfer anything in there, based on the command-line options.

     Options:
       --base           Base directory to copy files to on remote machine.
                            Defaults to the same path on remote machine as on this one.
       --blacklist      Do not copy files that match this regular expression.
       --help|-?        This help message.
       --hostname       Remote machine to copy files to.
                            Defaults to ENV{GIT_SCP_HOSTNAME} if available.
       --interactive    Choose files to transfer interactively.
       --modified_only  Copy only those files which are marked as 'modified' by git and not new or deleted.
       --print_only|-n  Dry-run. Do not copy, just print what would happen.
       --regex          Copy only the files that match this regular expression.
                            This is the default option.
       --ssh_options    Use these options when copying.
                            Defaults to ENV{GIT_SCP_SSH_OPTIONS}.
       --username       Use this username on remote machine when copying the files.
                            Defaults to ENV{GIT_SCP_HOSTNAME} or the current username.
       --verbose        Print extra information.

=head1 NORMAL USAGE

The purpose of this script is to copy any of the files that are currently modified or added to your
local repository to a remote copy of the repository. This is to facilitate working locally in a
cloud-dev environment.

To set this script up, it will be useful for you to set two environment variables:

    GIT_SCP_HOSTNAME
    GIT_SCP_USERNAME

=cut

use Cwd qw/getcwd/;
use File::Basename qw/basename dirname/;
use Getopt::Long;
use Pod::Usage qw/pod2usage/;

use warnings;
use strict;

my $BLACKLIST = [];

my $current_dir = `pwd`;
chomp $current_dir;

my($interactive, $modified_only, $print_only, $regex, $staged_only, $verbose);

my %transferred;

my $hostname = $ENV{GIT_SCP_HOSTNAME};
my $username = $ENV{GIT_SCP_USERNAME} || $ENV{USER};
my $ssh_options = $ENV{GIT_SCP_SSH_OPTIONS} || '';
my $filenames_given = 0;

# assume mirrored directory setups
(my $base_directory) = $current_dir =~ m{$ENV{HOME}/(.*)};
GetOptions(
    'base=s'        => \$base_directory,
    'blacklist=s@'  => \$BLACKLIST,
    'help|?'        => sub { pod2usage(); },
    'hostname=s'    => \$hostname,
    'interactive|i' => \$interactive,
    'modified_only' => \$modified_only,
    'print_only|n'  => \$print_only,
    'regex=s'       => \$regex,
    'ssh_options=s' => \$ssh_options,
    'staged_only'   => \$staged_only,
    'username=s'    => \$username,
    'verbose'       => \$verbose
);

# make sure all printing happens when we're interactive.
$verbose = 1 if $interactive;

if( @ARGV and not $regex and not -e $ARGV[0] ){
    $regex = shift;
}

unless( $hostname ){
    print STDERR "You must give this script a --hostname!\n";
    exit 1;
}

$BLACKLIST = join '|', @$BLACKLIST;
if( $BLACKLIST ) {
    $BLACKLIST = qr/$BLACKLIST/;
}

my $remote_base_directory = qq{/home/$username};
my $TRANSFER_TO_URL = qq{$hostname:} . join '/', $remote_base_directory, $base_directory;
$TRANSFER_TO_URL = "$username\@$TRANSFER_TO_URL" if $username;

if( defined $regex ) {
    my $cwd = getcwd();
    $regex =~ s{^$cwd\/}{};
    $regex = qr/\Q$regex\E/;
}

if( not -t STDIN ) {
    print "Processing filenames from STDIN\n" if $verbose;
    $filenames_given = 1;
    while(<>) {
        chomp;
        transfer_file_flow(filename => $_);
    }
}
elsif ( @ARGV ) {
    print "Processing filenames from the command line\n" if $verbose;
    $filenames_given = 1;
    foreach my $filename (@ARGV) {
        transfer_file_flow(filename => $filename);
    }
}
else {
    open PROC_OUT, 'git status --short |' or die "Couldn't deal with 'git status': $!";

    while( <PROC_OUT> ){
        my($x, $y, $filename) = m{^(.)(.)\s*(\S+)$};

        next if $modified_only and $y ne 'M' and $x ne 'M';
        next if $staged_only and $x eq ' ';

        transfer_file_flow(filename => $filename, delete => $y eq 'D');
    }
}

sub file_is_blacklisted {
    my $filename = shift;

    return 0 if not $BLACKLIST;
    return $filename =~ $BLACKLIST;
}

sub file_is_whitelisted {
    my $filename = shift;

    return 1 if not defined $regex;

    print STDERR "Attempting to match /$regex/ against $filename..." if $verbose;
    unless( $filename =~ /$regex/ ) {
        print " did NOT match!\n" if $verbose;
        return 0;
    }

    print "matched!\n" if $verbose;
    return 1;
}

sub transfer_file_flow {
    my %options = @_;

    my $filename = delete $options{filename} or die 'No filename provided for transfer!';
    my $deletion = delete $options{delete} ;

    print "Processing filename {$filename}\n" if $verbose;
    $filename =~ s{\\}{/}g;

    return if $transferred{$filename}++;
    return if file_is_blacklisted($filename);
    return unless file_is_whitelisted($filename);

    my $dirname = dirname($filename);
    $dirname = '' if $dirname eq '.';

    my $execute;
    if ( $deletion ) {
        $execute = qq{ssh $ssh_options $hostname $remote_base_directory/$filename};
    }
    else {
        $execute = qq{scp $ssh_options $filename $TRANSFER_TO_URL/$dirname};
    }

    print "Executing: $execute\n" if $verbose or $print_only;

    if ( not $filenames_given and $interactive ){
        my $input;
        print "\nWould you like me to execute this command (y/N)? ";
        $input = <STDIN>;

        if( $input =~ m{^[an]}i ){ #allow an abort
            return;
        }
    }

    if ( $deletion ) {
        print "DELETING $hostname:$remote_base_directory/$filename\n";
    }
    else {
        print "$filename => $TRANSFER_TO_URL/$dirname\n";
    }

    if ( not $print_only ) {
        system($execute);
    }
}
