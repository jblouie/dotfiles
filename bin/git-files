#!/usr/bin/env perl

=head1 NAME

    git-files - print the list of files associated with a commit or current status

=head1 SYNOPSIS

    git-files [commit]

     Options:
       --cached|staged  Print only the files that have been "git add"ed.
       --modified_only  Print only files that have been modified, not added or deleted.
       --oneline        Print files separated by spaces instead of newlines.
=cut


use warnings;
use strict;

use Getopt::Long;
use Pod::Usage qw/pod2usage/;

our($modified_only, $oneline, $staged_only, $verbose);
our $REPO_ROOT = execute_and_chomp('git rev-parse --show-toplevel');
our $CURRENT_DIR = execute_and_chomp('pwd');
our $REPO_DIR_DIFF = path_subtract($CURRENT_DIR, $REPO_ROOT);

GetOptions(
           'help|?'        => sub { pod2usage() },
           'modified_only' => \$modified_only,
           'oneline'       => \$oneline,
           'staged|cached' => \$staged_only,
           'verbose'       => \$verbose,
);

my $commit = shift;

if($commit){
    print "Running do_git_show with commit: '$commit'\n" if $verbose;
    do_git_show($commit);
}
else {
    do_git_status(modified_only => $modified_only, staged_only => $staged_only);
}


sub do_git_status {
    my $files_in_modified_state = 0;

    my @files;

    open PROC_OUT, "git status --short |" or die "Couldn't deal with 'git status': $!";
    while( <PROC_OUT> ){
        $files_in_modified_state = 1;
        my($x, $y, $filename) = m{^(.)(.)\s*(\S+)$};

        my $staged = $x ne ' ' && $x ne 'U';
        my $modified = $x eq 'M' || $y eq 'M';
        my $added = $x eq 'A' || $y eq 'A';
        my $deleted = $x eq 'D' || $y eq 'D';
        my $unmerged = $x eq 'U' && $y eq 'U';

        next if $modified_only and not $modified;
        next if $staged_only and not $staged;

        if( $oneline ){
            push @files, $filename;
        }
        else {
            print $filename, "\n";
        }

    }

    if( not $files_in_modified_state ){
        do_git_show();
    }
    elsif( $oneline ){
        print "@files\n";
    }
}

sub do_git_show {
    my $commit = shift || 'HEAD';
    my $command = 'git show --pretty="format:" --name-only';

    my $output = `$command $commit`;
    my @file_list = map {s{^$REPO_DIR_DIFF/?}{}; $_} split /\n+/, $output;
    @file_list = list_uniq(@file_list);

    if($oneline){
        print "@file_list\n";
    }
    else {
        print join("\n", @file_list), "\n";
    }
}

sub path_subtract {
    my($minuend, $subtrahend) = @_;

    $minuend =~ s{^$subtrahend/?}{};
    return $minuend;
}

sub execute_and_chomp {
    my $exec = shift;

    my $output = `$exec`;
    chomp $output;

    return $output;
}

sub list_uniq {
    my %seen;

    foreach my $item (@_) {
        $seen{$item}++;
    }

    return sort keys(%seen);
}
